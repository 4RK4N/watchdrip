plugins {
    id 'com.android.application'
}

repositories {
    maven {
        url "https://maven.google.com"
    }
    maven {
        url "https://jitpack.io"
    }
   /* flatDir {
        dirs 'libs'
    }*/
}

def generateVersionNumber = { ->
    StringBuilder stringBuilder = new StringBuilder()
    stringBuilder.append((new Date()).format('yyMMddHH'))
    return Integer.parseInt(stringBuilder.toString())
}

def generateVersionNumberString = { ->
    StringBuilder stringBuilder = new StringBuilder()
    stringBuilder.append((new Date()).format('yyMMddHH'))
    return stringBuilder.toString()
}

def generateTimestamp = { ->
    StringBuilder stringBuilder = new StringBuilder()
    stringBuilder.append((new Date()).getTime())
    stringBuilder.append("L");
    return stringBuilder.toString()
}

def generateRandomUUID = { ->
    StringBuilder stringBuilder = new StringBuilder();
    stringBuilder.append('"' + UUID.randomUUID().toString() + '"')
    return stringBuilder.toString()
}

def generateVersionName = { ->

    StringBuilder stringBuilder = new StringBuilder()
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--always'
            standardOutput = stdout
        }
        String commitObject = stdout.toString().trim()
        try {
            stdout = new ByteArrayOutputStream()
            exec {
                commandLine 'git', 'describe', '--tags'
                standardOutput = stdout
            }
            //stringBuilder.append(stdout.toString().trim())
            //stringBuilder.append("-")
        } catch (ignored) {
            // no tags
        }

        stringBuilder.append(commitObject)
        stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--show-toplevel'
            standardOutput = stdout
        }
        if (stdout.toString().trim().contains("xDrip-Experimental")) {
            stringBuilder.append("-experimental")
        }

        stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
            standardOutput = stdout
        }
        String branch = stdout.toString().trim()
        if (!branch.equals("master")) {
            stringBuilder.append('-')
            stringBuilder.append(branch)
        }

    } catch (ignored) {
        return "NoGitSystemAvailable"
    }
    stringBuilder.append('-')
    stringBuilder.append((new Date()).format('yyyy.MM.dd'))
    return stringBuilder.toString()
}

/**
 * Reads a property from the project settings (Gradle properties), the system environment or
 * from the file <code>local.properties</code>.
 *
 * @param propertyName the name of the property to read
 * @return the property value or an empty string
 */
def getLocalProperty(String propertyName) {
    def propVal = project.findProperty(propertyName)
    if (propVal == null) {
        propVal = System.getenv(propertyName)
        if (propVal == null)
            propVal = System.getenv(propertyName.toLowerCase())
        if (propVal == null)
            propVal = System.getenv(propertyName.toUpperCase())
        if (propVal == null) {
            def propsFile = rootProject.file('local.properties')
            if (propsFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propsFile))
                propVal = props[propertyName]
            } else {
                propVal = ""
            }
        }
    }
    return propVal
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.thatguysservice.huami_xdrip"
        minSdkVersion 18
        targetSdkVersion 30
        versionCode 1
        versionName generateVersionName()
        buildConfigField "int", "buildVersion", generateVersionNumberString()
        buildConfigField "String", "buildUUID", generateRandomUUID()
        buildConfigField "long", "buildTimestamp", generateTimestamp()
        buildConfigField "int", "targetSDK", targetSdkVersion.mApiLevel.toString()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path file('src/main/c/CMakeLists.txt')
        }
    }
}

dependencies {
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.navigation:navigation-fragment:2.3.5'
    implementation 'androidx.navigation:navigation-ui:2.3.5'

    // you will want to install the android studio lombok plugin
    compileOnly 'org.projectlombok:lombok:1.18.10'
    //  compileOnly 'javax.annotation:javax.annotation-api:1.3.2'
    annotationProcessor "org.projectlombok:lombok:1.18.10"

//fix The number of method references in a .dex file cannot exceed 64K error
    implementation 'com.squareup.okhttp:okhttp:2.7.5'
    implementation 'com.android.support:multidex:1.0.3'
    implementation "androidx.fragment:fragment:1.3.6"
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.guava:guava:24.1-android'
    implementation 'org.apache.commons:commons-text:1.3'
    implementation 'com.activeandroid:thread-safe-active-android:3.1.1'
    implementation "com.polidea.rxandroidble2:rxandroidble:1.12.1"
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
   // implementation(name: 'hellocharts-library-release', ext: 'aar')
    implementation("com.github.lecho:hellocharts-library:1.5.8")
}
